defmodule PearlWeb.Components.CVUpload do
  @moduledoc """
  Attendee Curriculum Vitae upload component.
  """
  use PearlWeb, :live_component

  alias Pearl.Accounts
  alias Pearl.Uploaders.CV

  import PearlWeb.Components.ImageUploader
  import PearlWeb.Components.Button

  attr :in_app, :boolean, default: false

  @impl true
  def render(assigns) do
    ~H"""
    <div>
      <.simple_form
        for={@form}
        id="attendee-form"
        phx-target={@myself}
        phx-change="validate"
        phx-submit="save"
      >
        <div class="flex flex-col md:flex-row w-full gap-4">
          <div class="w-full space-y-2">
            <.image_uploader
              class="h-40 rounded-2xl border-light/20! hover:border-primary/50! hover:shadow-[0_0_20px_2px] hover:shadow-primary/25 transition-all! duration-300! hover:bg-primary/10!"
              upload={@uploads.cv}
            >
              <:placeholder>
                <div class="select-none flex flex-col gap-2 items-center text-lightMuted dark:text-darkMuted">
                  <.icon name="hero-arrow-up-tray" class="w-12 h-12" />
                  <p class="px-4 text-center">Faz upload do teu CV</p>
                </div>
              </:placeholder>
            </.image_uploader>
            <div :if={@current_user.cv} class="pt-2">
              <p class="text-sm text-lightSahde dark:text-darkMuted">
                {gettext("CV atual: ")}<span class="text-lightMuted"><%= @current_user.cv.file_name %></span>
              </p>
              <p class="text-sm text-lightMuted dark:text-darkMuted">
                {gettext("Podes substituir o teu CV atual carregando um novo.")}
              </p>
            </div>
          </div>
        </div>
        <:actions>
          <%= if @in_app do %>
            <.action_button title="Upload" phx-disable-with="Uploading..." />
          <% else %>
            <.backoffice_button phx-disable-with="Uploading...">Upload</.backoffice_button>
          <% end %>
        </:actions>
      </.simple_form>
    </div>
    """
  end

  @impl true
  def mount(socket) do
    {:ok,
     socket
     |> assign(:uploaded_files, [])
     |> allow_upload(:cv,
       accept: CV.extension_whitelist(),
       max_entries: 1,
       max_file_size: 10_000_000
     )}
  end

  @impl true
  def update(%{current_user: user} = assigns, socket) do
    {:ok,
     socket
     |> assign(assigns)
     |> assign_new(:form, fn ->
       to_form(Accounts.change_user_profile(user))
     end)}
  end

  @impl true
  def handle_event("validate", _params, socket) do
    {:noreply, socket}
  end

  def handle_event("save", %{}, socket) do
    save_user(socket, %{})
  end

  defp save_user(socket, user_params) do
    case Accounts.update_user(socket.assigns.current_user, user_params) do
      {:ok, user} ->
        case consume_pdf_data(user, socket) do
          {:ok, user} ->
            {:noreply,
             socket
             |> put_flash(:info, "CV carregado com sucesso.")
             |> assign(current_user: Map.put(socket.assigns.current_user, :cv, user.cv))
             |> push_patch(to: socket.assigns.patch)}

          {:error, reason} ->
            {:noreply,
             socket |> put_flash(:error, reason) |> push_patch(to: socket.assigns.patch)}
        end

      {:error, %Ecto.Changeset{} = changeset} ->
        {:noreply, assign(socket, form: to_form(changeset))}
    end
  end

  defp consume_pdf_data(user, socket) do
    consume_uploaded_entries(socket, :cv, fn %{path: path}, entry ->
      Accounts.update_user_cv(user, %{
        "cv" => %Plug.Upload{
          content_type: entry.client_type,
          filename: entry.client_name,
          path: path
        }
      })
      |> case do
        {:ok, user} ->
          {:ok, user}

        {:error, _changeset} ->
          {:error, "Ocorreu um erro ao atualizar o utilizador."}
      end
    end)
    |> case do
      [] ->
        {:error, "Selecione um ficheiro para carregar."}

      [error: _message] ->
        {:error, "Ocorreu um erro ao carregar o ficheiro."}

      [user] ->
        {:ok, user}
    end
  end
end
